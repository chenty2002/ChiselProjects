// This Verilog module describes a simple finite state machine for
// the computation of the so-called rollercoaster numbers.
// Given an initial number n[0] > 0, the rollercoaster number
// sequence (n[i]) starting at n[0] is given by these rules:
//   if n[i] is even, n[i+1] = n[i]/2;
//   if n[i] is odd,  n[i+1] = n[i]*3+1.
// The sequences for most (small) numbers eventually reach the
// cycle (4,2,1).
// This finite state machine has a state register storing n[i].
// Since the register is finite, only a few positive integers can
// be represented. Therefore 0 is used as trap state to indicate
// overflow.
//
// This description intentionally avoids the use of '*' and '/'.
//
// Author: Fabio Somenzi <Fabio@Colorado.EDU>

`let MSB = 24

module rollercoasterNumbers(clock,numOut);
    input	    clock;
    output [`MSB:0] numOut;
    reg [`MSB:0]    numOut;

    wire [`MSB+2:0] tmp;

    initial begin
`ifdef VIS
	`for (i=0; i <= `MSB; i++)
	numOut[`i] = $ND(0,1);	
	`endfor
`else
	numOut = {`MSB+1{1'bx}};
`endif
    end

    // Compute n[i] * 3 + 1.
    assign tmp = {2'b0,numOut} + {1'b0,numOut,1'b1};

    always @ (posedge clock)
	if (numOut[0]) begin
	    // Check overflow.
	    numOut = (tmp[`MSB+2] | tmp[`MSB+1]) ? 0 : tmp[`MSB:0];
	end else begin
	    // Divide by 2.
	    numOut = {1'b0,numOut[`MSB:1]};
	end

endmodule // rollercoasterNumbers
