//-----------------------------------------------------------------
//
// module IDU: Instruction Decode Unit for subset of Alpha ISA
//
// Determine the control signals necessary for executing an instruction.
//
// Inputs:
// Clk         global clock;
//             from: CHIP
// Work        1..valid operation here / 0..bubble here;
//             from: PCU
// Step        advance pipeline on next clk;
//             from: PCU
// Reset       processor reset;
//             from: CHIP
// Insn        insn bus;
//             insn to decode;
//             from: IFU
// PC          program counter (PC+4);
//             muxed onto Operand2 for PC relative computations;
//             from: IFU
// RegAValue   register Ra value;
//             from: FRU; asynchronous; operates in same cycle as IDU;
// RegBValue   register Rb value;
//             from: FRU; asynchronous; operates in same cycle as IDU;
//
// Outputs:
// Const       constant from immediate field;
//             only valid for insns which define an immediate operand
//             to: EXU; immediate field
// RegA        register number source operand Ra + valid bit;
//             extended with valid bit; msb = 0 / 1 ... invalid / valid
//             to: FRU; asynchronous; operates in same cycle as IDU
// RegB        register number source operand Rb + valid bit;
//             extended with valid bit; msb = 0 / 1 ... invalid / valid
//             to: FRU; asynchronous; operates in same cycle as IDU
// RegDest     register number destination operand + valid bit;
//             normally this is Rc; however, for LDx insns it is Ra;
//             to: EXU; needed for forwarding by FRU and WB stage
// Operand1    operand 1 value;
//             handed through from FRU;
//             to: EXU;
// Operand2    operand 2 value;
//             either Rb value from FRU or PC from IFU;
//             to: EXU;
// OpCode      opcode from instruction encoding;
//             to: EXU; insn opcode field
// FctCode     function code from instruction encoding;
//             to: EXU; operate insn function code field
// Decode      decoded instruction bits (see includes/decode.v)
//             to: ALU; pass to MAU
//                 PCU; pipeline advancing decisions
// Exception   exception request from IDU;
//             can signal EXC_PAL (palcode opcode),
//             EXC_RESV (reserved opcode),
//             EXC_FP (floating point opcode),
//             EXC_LDLSTC (LD_L/ST_C opcode);
//             normal condition signalled by EXC_NONE;
//             to: PCU; exception handling
//
// -------------------------------------------------------------

/*
//-----------------------------------------------------------------------
// constants definitions
//
// Artur Klauser
//
// $Author: klauser $
// $Date: 97/03/15 01:14:37 $
// $Revision: 1.4 $
// $Log:	const.v,v $
# Revision 1.4  97/03/15  01:14:37  01:14:37  klauser (Artur Klauser)
# bugfix: can't use computation in constant if used as `cons'b0
# 
// Revision 1.3  1997/03/13 11:04:06  toavs
// fixed cvs log commenting to ust the right kind of verilog comment
// to avoid log messages from screwing up verilog compile as syntax errors
//
// Revision 1.2  97/03/13  11:00:37  11:00:37  toavs (Coy Toavs)
// fixed commenting to keep cvs logs from screwing things up....
// NEED To Use comment around the section so that new lines that cvs
// adds are commented out automatically
// 
// Revision 1.1  97/03/10  20:46:28  20:46:28  klauser (Artur Klauser)
// include files for IDU and EXU
// 
//-----------------------------------------------------------------------
*/

//-----------------------------------------------------------------------
// data sizes
//-----------------------------------------------------------------------
`define B       8     // byte
`define W      16     // word
`define L      32     // longword
`define Q      32//64     // quadword
`define C      21     // constant

//-----------------------------------------------------------------------
// MSBs
//-----------------------------------------------------------------------
`define BM     7      // byte msb
`define WM     15     // word msb
`define LM     31     // longword msb
`define QM     31//63     // quadword msb try with 31
`define CM     20     // constant msb

//-----------------------------------------------------------------------
// busses
//-----------------------------------------------------------------------
`define DATAX  [`Q:0]     // extended data busses (include a carry bit)
`define DATAQ  [`QM:0]    // quadword data busses
`define DATAL  [`LM:0]    // longword data busses
`define DATAW  [`WM:0]    // word data busses
`define DATAB  [`BM:0]    // byte data busses
`define INSN   [`LM:0]    // instruction
`define CONST  [`C:0]     // valid bit + constants and displacements
`define CONSTN [`CM:0]    // immediate constants and displacements
`define CONSTM [`CM]      // constant msb
`define CONSTV [`C]       // constant valid bit
`define EXC    [ 3:0]     // exceptions
`define EXCV   [ 3]       // exception valid bit
`define OPC    [ 5:0]     // opcode
`define FCT    [ 6:0]     // function code
`define REG    [ 5:0]     // valid bit + register numbers
`define REGN   [ 4:0]     // register numbers
`define REGV   [ 5]       // register valid bit
`define PCADR  [`QM:0]    // program counter

//-----------------------------------------------------------------------
// misc
//-----------------------------------------------------------------------
`define FALSE  1'b0  // boolean true
`define TRUE   1'b1  // boolean false

`define DELTA  1     // delay for behavioral sequential modelling

//-----------------------------------------------------------------------
// Exception Codes
// msb clear ... no exception
// msb set ..... exception type indicated by lower significant bits
//-----------------------------------------------------------------------
`define EXC_NONE    4'b0_000 // no exception
`define EXC_OVFL    4'b1_000 // overflow (from ALU)
`define EXC_PAL     4'b1_001 // pal call (from IDU)
`define EXC_RESV    4'b1_010 // reserved opcode (from IDU)
`define EXC_FP      4'b1_011 // floating point opcode (from IDU)
`define EXC_UDEF    4'b1_100 // undefined function code (from ALU)
`define EXC_LDLSTC  4'b1_101 // LDxL / STxC opcode (from IDU)

/*
//-----------------------------------------------------------------------
// opcode definitions
//
// DEC Alpha ISA
//
// Artur Klauser
//
// $Author: klauser $
// $Date: 97/04/04 00:44:59 $
// $Revision: 1.3 $
// $Log:	opcode.v,v $
# Revision 1.3  97/04/04  00:44:59  00:44:59  klauser (Artur Klauser)
# changed JMP_* function codes to 2 bits instead of 7 bits
# 
// Revision 1.2  1997/03/13 11:35:33  toavs
// fixed comment around cvs log so that the # comments cvs inserts
// don't screw up verilog as syntax errors
//
# Revision 1.1  97/03/10  20:46:28  20:46:28  klauser (Artur Klauser)
# include files for IDU and EXU
# 
//-----------------------------------------------------------------------
*/

//----------------------------------------------------------------------------
// Instruction Encoding
//----------------------------------------------------------------------------
//
// All instructions have a 6 bit opcode field which determines the
// further encoding of the instructions. Additional fields are available
// in each encoding format as described below.
// Instruction encoding formats:
//   Bra .. branch encoding:  Ra, 21bit signed displacement
//   Mem .. memory encoding:  Ra, Rb, 16bit signed displacement
//   Mfc .. memory encoding:  Ra, Rb, 16bit function code
//   Mbr .. memory encoding:  Ra, Rb, 2bit flavor + 14bit cache hint
//   Opr .. operate encoding: Ra, Rb, Rc, 7bit function code
//                        or  Ra, 8bit unsigned literal, Rc, 7bit FctCode
//
//----------------------------------------------------------------------------

//----------------------------------------------------------------------------
//
// opcodes
//
//----------------------------------------------------------------------------

//----------------------------------------------------------------------------
// integer operation opcodes
//----------------------------------------------------------------------------
`define OP_INTA   6'h10  // (Opr) integer arithmetic operations
`define OP_INTL   6'h11  // (Opr) integer logical operations
`define OP_INTS   6'h12  // (Opr) integer shift operations
`define OP_INTM   6'h13  // (Opr) integer multiply operations

//----------------------------------------------------------------------------
// memory operation opcodes
//----------------------------------------------------------------------------
//--- LD_L / ST_C --- not implemented ---
`define OP_LDA    6'h08  // (Mem) load address
`define OP_LDAH   6'h09  // (Mem) load address high
`define OP_LDL    6'h28  // (Mem) load sign-extended longword
`define OP_LDQ    6'h29  // (Mem) load quadword
`define OP_LDL_L  6'h2a  // (Mem) load sign-extended longword locked
`define OP_LDQ_L  6'h2b  // (Mem) load quadword locked
`define OP_LDQ_U  6'h0b  // (Mem) load quadword unaligned
`define OP_STL    6'h2c  // (Mem) store longword
`define OP_STQ    6'h2d  // (Mem) store quadword
`define OP_STL_C  6'h2e  // (Mem) store longword conditional
`define OP_STQ_C  6'h2f  // (Mem) store quadword conditional
`define OP_STQ_U  6'h0f  // (Mem) store quadword unaligned

//----------------------------------------------------------------------------
// branch and jump operation opcodes
//----------------------------------------------------------------------------
`define OP_BLBC   6'h38  // (Bra) branch if reg low bit is clear
`define OP_BEQ    6'h39  // (Bra) branch if reg equal to zero
`define OP_BLT    6'h3a  // (Bra) branch if reg less than zero
`define OP_BLE    6'h3b  // (Bra) branch if reg less than or equal to zero
`define OP_BLBS   6'h3c  // (Bra) branch if reg low bit is set
`define OP_BNE    6'h3d  // (Bra) branch if reg not equal to zero
`define OP_BGE    6'h3e  // (Bra) branch if reg greater than or equal to zero
`define OP_BGT    6'h3f  // (Bra) branch if reg greater than zero
`define OP_BR     6'h30  // (Bra) uncond. branch (PC relative)
`define OP_BSR    6'h34  // (Bra) uncond. branch to subroutine (PC relative)
`define OP_JMP    6'h1a  // (Mbr) jump register indirect

//----------------------------------------------------------------------------
// misc operation opcodes
//----------------------------------------------------------------------------
`define OP_MISC   6'h18  // (Mfc) miscellaneous instruction opcodes

//----------------------------------------------------------------------------
//
// function codes
//
//----------------------------------------------------------------------------

//----------------------------------------------------------------------------
// integer arithmetic function codes (OP_INTA opcode)
//----------------------------------------------------------------------------
`define INTA_ADDL     7'h00  // add longword
`define INTA_ADDLV    7'h40  // add longword (check overflow)
`define INTA_S4ADDL   7'h02  // scaled_4 add longword
`define INTA_S8ADDL   7'h12  // scaled_8 add longword
`define INTA_ADDQ     7'h20  // add quadword
`define INTA_ADDQV    7'h60  // add quadword (check overflow)
`define INTA_S4ADDQ   7'h22  // scaled_4 add quadword
`define INTA_S8ADDQ   7'h32  // scaled_8 add quadword
`define INTA_CMPBGE   7'h0f  // compare byte
`define INTA_CMPEQ    7'h2d  // compare signed quadword equal
`define INTA_CMPLT    7'h4d  // compare signed quadword less than
`define INTA_CMPLE    7'h6d  // compare signed quadword less than or equal
`define INTA_CMPULT   7'h1d  // compare unsigned quadword less than
`define INTA_CMPULE   7'h3d  // compare unsigned quadword less than or equal
`define INTA_SUBL     7'h09  // subtract longword
`define INTA_SUBLV    7'h49  // subtract longword (check overflow)
`define INTA_S4SUBL   7'h0b  // scaled_4 subtract longword
`define INTA_S8SUBL   7'h1b  // scaled_8 subtract longword
`define INTA_SUBQ     7'h29  // subtract quadword
`define INTA_SUBQV    7'h69  // subtract quadword (check overflow)
`define INTA_S4SUBQ   7'h2b  // scaled_4 subtract quadword
`define INTA_S8SUBQ   7'h3b  // scaled_8 subtract quadword

//----------------------------------------------------------------------------
// integer logical function codes (OP_INTL opcode)
//----------------------------------------------------------------------------
`define INTL_AND      7'h00  // and
`define INTL_BIC      7'h08  // and not
`define INTL_BIS      7'h20  // or
`define INTL_EQU      7'h48  // xnor
`define INTL_ORNOT    7'h28  // or not
`define INTL_XOR      7'h40  // xor
`define INTL_CMOVEQ   7'h24  // cmove if reg equal to zero
`define INTL_CMOVGE   7'h46  // cmove if reg greater than or equal to zero
`define INTL_CMOVGT   7'h66  // cmove if reg greater than zero
`define INTL_CMOVLBC  7'h16  // cmove low bit clear
`define INTL_CMOVLBS  7'h14  // cmove low bit set
`define INTL_CMOVLE   7'h64  // cmove if reg less than or equal to zero
`define INTL_CMOVLT   7'h44  // cmove if reg less than zero
`define INTL_CMOVNE   7'h26  // cmove if reg not equal to zero
`define INTL_AMASK    7'h61  // ??? found in alpha/inst.h
`define INTL_IMPLVER  7'h6c  // ??? found in alpha/inst.h

//----------------------------------------------------------------------------
// integer shift function codes (OP_INTS opcode)
//----------------------------------------------------------------------------
`define INTS_EXTBL    7'h06  // extract byte low
`define INTS_EXTWL    7'h16  // extract word low
`define INTS_EXTLL    7'h26  // extract longword low
`define INTS_EXTQL    7'h36  // extract quadword low
`define INTS_EXTWH    7'h5a  // extract word high
`define INTS_EXTLH    7'h6a  // extract longword high
`define INTS_EXTQH    7'h7a  // extract quadword high
`define INTS_INSBL    7'h0b  // insert byte low
`define INTS_INSWL    7'h1b  // insert word low
`define INTS_INSLL    7'h2b  // insert longword low
`define INTS_INSQL    7'h3b  // insert quadword low
`define INTS_INSWH    7'h57  // insert word high
`define INTS_INSLH    7'h67  // insert longword high
`define INTS_INSQH    7'h77  // insert quadword high
`define INTS_MSKBL    7'h02  // mask byte low
`define INTS_MSKWL    7'h12  // mask word low
`define INTS_MSKLL    7'h22  // mask longword low
`define INTS_MSKQL    7'h32  // mask quadword low
`define INTS_MSKWH    7'h52  // mask word high
`define INTS_MSKLH    7'h62  // mask longword high
`define INTS_MSKQH    7'h72  // mask quadword high
`define INTS_ZAP      7'h30  // zero bytes
`define INTS_ZAPNOT   7'h31  // zero bytes not
`define INTS_SLL      7'h39  // shift left logical
`define INTS_SRA      7'h3c  // shift right arithmetic
`define INTS_SRL      7'h34  // shift right logical

//----------------------------------------------------------------------------
// integer multiply function codes (OP_INTM opcode)
//----------------------------------------------------------------------------
`define INTM_MULL     7'h00  // multiply longword
`define INTM_MULLV    7'h40  // multiply longword (check overflow)
`define INTM_MULQ     7'h20  // multiply quadword
`define INTM_MULQV    7'h60  // multiply quadword (check overflow)
`define INTM_UMULH    7'h30  // unsigned quadword multiply high

//----------------------------------------------------------------------------
// jump function codes (OP_JMP opcode)
//----------------------------------------------------------------------------
`define JMP_JMP            2'h0 // jump
`define JMP_JSR            2'h1 // jump subroutine
`define JMP_JSR_COROUTINE  2'h3 // jump coroutine
`define JMP_RET            2'h2 // return

//----------------------------------------------------------------------------
// miscellaneous function codes (OP_MISC opcode)
//----------------------------------------------------------------------------
//--- not implemented ---
`define MISC_MB      16'h4000  // memory barrier
`define MISC_WMB     16'h4400  // write memory barrier
`define MISC_EXCB    16'h0400  // exception barrier
`define MISC_TRAPB   16'h0000  // trap barrier
`define MISC_RC      16'he000  // read and clear
`define MISC_RS      16'hf000  // read and set
`define MISC_SEXTB   16'h0000  // ??? found in alpha/inst.h
`define MISC_SEXTW   16'h0001  // ??? found in alpha/inst.h
//--- correct implementation optional ---
`define MISC_FETCH   16'h8000  // prefetch data
`define MISC_FETCH_M 16'ha000  // prefetch data, modify intent
`define MISC_RPCC    16'hc000  // read process cycle counter

/*
//----------------------------------------------------------------------------
//
// decoding definitions
//
// This file contains decoding information for the original Alpha 
// insn encoding as well as for the processor's internal decoded format
//
// Artur Klauser
//
// $Author: klauser $
// $Date $
// $Revision: 1.3 $
// $Log:	decode.v,v $
# Revision 1.3  97/03/18  15:36:10  15:36:10  klauser (Artur Klauser)
# additional field definitions
# 
// Revision 1.2  1997/03/13 11:04:08  toavs
// fixed cvs log commenting to ust the right kind of verilog comment
// to avoid log messages from screwing up verilog compile as syntax errors
//
# Revision 1.1  97/03/11  01:47:04  01:47:04  klauser (Artur Klauser)
# decoding information for Alpha insn encoding and
# processor internal encoding
# 
//----------------------------------------------------------------------------
*/

//----------------------------------------------------------------------------
// Alpha encoding
// instruction encoding field positions
//----------------------------------------------------------------------------
`define POS_OPCODE    31:26
`define POS_FUNCTION  11: 5
`define POS_HINT      15:14
`define POS_REGA      25:21
`define POS_REGB      20:16
`define POS_REGC       4: 0
`define POS_IMMEDIATE 20: 0
`define POS_DISP      15: 0
`define POS_DISPHI    15
`define POS_LITERAL   20:13

//----------------------------------------------------------------------------
// processor internal enconding
// decoded information
//
// Always use these constants when querying information on the internal
// decoded bus. The bus has to be declared with size `DEC
//----------------------------------------------------------------------------
                               // vertical encoding (1 bit per category)
`define DEC_FCT  2             // decoder function codes (msb)
                               // horizontal encoding (shared bits)
`define DEC_ALU  `DEC_FCT+1    // is ALU operation
`define DEC_CTR  `DEC_ALU+1    // is control transfer operation
`define DEC_MEM  `DEC_CTR+1    // is memory operation

`define DEC_HOR  `DEC_MEM:`DEC_ALU // horizontal encoding part
`define DEC_VERT `DEC_FCT:0        // vertical encoding part

`define DEC      [`DEC_MEM:0]  // bus size definition

//----------------------------------------------------------------------------
// vertically encoded information is only valid when the respective
// horizontally encoded group is active;
// ie. DEC_MEM_xx is only valid when DEC_MEM is active;
//----------------------------------------------------------------------------

//----------------------------------------------------------------------------
// function codes for memory operations
//----------------------------------------------------------------------------
`define DEC_MEM_ST    0        // store..1 / load..0
`define DEC_MEM_QW    1        // quadword..1 / longword..0
`define DEC_MEM_ACC   2        // memory access..1 / addr.computation..0

//----------------------------------------------------------------------------
// function codes for control transfer operations
//----------------------------------------------------------------------------
`define DEC_CTR_COND  0        // conditional..1 / unconditional..0
`define DEC_CTR_PC    1        // PCrelative..1 / Reg.Indirect..0

//----------------------------------------------------------------------------
// function codes for ALU operations
//----------------------------------------------------------------------------
`define DEC_ALU_MULT  0        // multiply..1 / other..0


`ifdef DEBUG_IDU
`else
`define DEBUG_IDU 0
`endif
//`define IDU_DEBUG if(`DEBUG_IDU) $display

module idu (Clk, iWork, iStep, iReset,iInsn,iPC,iRegAValue,iRegBValue,Const, 
	    Operand1, Operand2, RegDest, OpCode, FctCode, Decode, Exception, 
	    RegA, RegB);

  // Inputs
  input         Clk,            // global clock
                iWork,           // valid operation here / bubble here
               iStep,           // latch new input on clk
                iReset;          // processor reset
  input   `INSN iInsn;           // insn bus from Ifetch
  input  `PCADR iPC;             // program counter (PC+4)
  input  `DATAQ iRegAValue,      // register Ra value from FRU
                iRegBValue;      // register Rb value from FRU

  // Outputs
  output `CONST Const;          // immediate operand (sign extended)
  output   `REG RegA,           // register number source operand Ra
                RegB,           // register number source operand Rb
                RegDest;        // register number destination operand
  output `DATAQ Operand1,       // operand 1 value
                Operand2;       // operand 2 value
  output   `OPC OpCode;         // opcode
  output   `FCT FctCode;        // function code (subcode of opcode)
  output   `DEC Decode;         // decoded insn bits
  output   `EXC Exception;      // exception request from IDU

  //
  // delayed internal wires
  //
  // Inputs
  wire          I_Work,
                I_Step,
                I_Reset;
  wire    `INSN I_Insn;
  wire    `PCADR I_PC;
  wire    `DATAQ I_RegAValue,
                I_RegBValue;

  // Outputs
  wire   `CONST I_Const;
  wire     `REG I_RegA,
                I_RegB,
                I_RegDest;
 wire  `DATAQ 	I_Operand1,
                I_Operand2;
 wire      `OPC I_OpCode;
 wire      `FCT I_FctCode;
 wire      `DEC I_Decode;
 wire      `EXC I_Exception;

//
  // registered internal wires
  //
  wire           R_Work;         // latched work signal
  wire    `INSN R_Insn;         // latched instruction
  wire    `PCADR R_PC;           // latched PC


   reg 		 Work,
                Step,
                Reset;
   reg `INSN 	 Insn;
   reg `PCADR 	 PC;
   reg `DATAQ 	 RegAValue,
                RegBValue;
  
   initial begin
      Work = 0;
      Step = 0;     
      Reset= 1;
      Insn = 0;
      PC = 0;
      RegAValue = 0;
      RegBValue = 0;
   end // initial begin
   
always @(posedge Clk) begin
   Work = iWork;
   Step = iStep;
   Reset = iReset;
   Insn = iInsn;
   PC = iPC;
   RegAValue = iRegAValue;
   RegBValue = iRegBValue;
end // always @ (posedge Clk)
   
   //
  // assigning delayed wires
  //
  // Inputs
   assign 	 I_Work      = Work;
  assign I_Step      = Step;
  assign I_Reset     = Reset;
  assign I_Insn      = Insn;
  assign I_PC        = PC;
  assign I_RegAValue = RegAValue;
  assign I_RegBValue = RegBValue;



  // Outputs
  assign Const       = I_Const;
  assign RegA        = I_RegA;
  assign RegB        = I_RegB;
  assign RegDest     = I_RegDest;
  assign Operand1    = I_Operand1;
  assign Operand2    = I_Operand2;
  assign OpCode      = I_OpCode;
  assign FctCode     = I_FctCode;
  assign Decode      = I_Decode;
  assign Exception   = I_Exception;

     
  //
  // instantiations of sequential and combinational subblocks
  //
   IDU_Pipeline Pipeline( Clk, I_Step, I_Reset,I_Work, I_Insn, I_PC,
			  R_Work, R_Insn, R_PC);
  IDU_Logic Logic(R_Work, R_Insn, R_PC,I_RegAValue, I_RegBValue, 
		   I_Const, I_Operand1, I_Operand2, I_RegDest, I_OpCode, 
		   I_FctCode,I_Decode, I_Exception,I_RegA, I_RegB  
		   /* asynchronous to FRU*/ );
  //------------------------------------------------------------------------
  // debug decoder - display disassembled insn
  //------------------------------------------------------------------------
/*  `include "disassemble.v"
 always @(posedge Clk) begin// 
 if (`DEBUG_IDU) begin
 if (R_Work & test.speed_racer.PCU_Step_EXU) begin
 $write(  "IDU: %h %h   %h/%h ", R_PC-4, R_Insn, OpCode, FctCode);
 Disassemble(OpCode, FctCode, Decode, Const,// 
 RegA, RegB, RegDest);// 
 $write(  "   (%0s)", DecodeStr(Decode));
 $display;
      end
    end
  end
 */
endmodule			// idu


//----------------------------------------------------------------------------
// synchronous interface stage of IDU
// latch incoming signals on rising edge of clock if pipeline stage is enabled
//----------------------------------------------------------------------------
 module IDU_Pipeline(Clk, iStep, iReset,iWork,iInsn,iPC,R_Work,R_Insn, R_PC );

  // Inputs
  input         Clk,            // system clock
                iStep,           // latch new input on clk
                iReset;          // reset
  input         iWork;           // valid operation here / bubble here
  input   `INSN iInsn;           // instruction bus
  input  `PCADR iPC;             // program counter (PC+4)

  // Outputs
  output        R_Work;         // latched work
  output  `INSN R_Insn;         // latched instruction
  output `PCADR R_PC;           // latched PC

  //
  // internal registers
  //
  reg           R_Work;
  reg     `INSN R_Insn;
  reg    `PCADR R_PC;

   reg          Step,           // latch new input on clk
                Reset;          // reset
   reg 		Work;           // valid operation here / bubble here
   reg `INSN 	Insn;           // instruction bus
   reg `PCADR 	PC;             // program counter (PC+4)

   initial begin
      R_Insn = 32'h0000_0000;
      R_PC =  32'h0000_0000;
      R_Work = 1'b0;
      Step = 0;
      Reset = 1;
      Work = 0;
      Insn = 0;
      PC = 0;
   end // initial begin
   
  //
  // When reset, purge pipeline stage
  //

   always @(posedge Clk) begin
      Step = iStep;
      Reset = iReset;
      Work = iWork;
      Insn = iInsn;
      PC = iPC;
   end // always @ (posedge Clk)
   
  always @(Reset) begin
    if (Reset) begin
      assign R_Work = `FALSE;
    end 
  end // always @ (Reset)
   
  //
  // latch signals
  //
  always @(posedge Clk) begin
    R_Work = Work;
    if (Step) begin
      R_Insn = Insn;
      R_PC   = PC;
    end
  end

endmodule // IDU_Pipeline


//----------------------------------------------------------------------------
//
// This module determines the control signals necessary
// for execution of the present instruction.
//
//----------------------------------------------------------------------------

 module IDU_Logic(iWork, iInsn, iPC,iRegAValue, iRegBValue,Const, Operand1, 
		  Operand2, RegDest, OpCode, FctCode, Decode, Exception,
		  RegA, RegB);

  // Inputs
  input         iWork;           // valid insn in pipeline
  input   `INSN iInsn;           // instruction bus
  input  `PCADR iPC;             // program counter (PC+4)
  input  `DATAQ iRegAValue,      // register Ra value from FRU
                iRegBValue;      // register Rb value from FRU

  // Outputs
  output `CONST Const;          // immediate operand
  output   `REG RegA,           // register number operand A
                RegB,           // register number operand B
                RegDest;        // register number destination operand
  output `DATAQ Operand1,       // operand 1 value
                Operand2;       // operand 2 value
  output   `OPC OpCode;         // opcode
  output   `FCT FctCode;        // opcode sub-function
  output   `DEC Decode;         // decoded insn
  output   `EXC Exception;      // exception request from IDU

  reg    `CONST Const;
  reg      `REG RegA_int,
                RegB_int,
                RegDest_int;
  reg    `DATAQ Operand1,
                Operand2;
  reg      `OPC OpCode;
  reg      `FCT FctCode;
  reg      `DEC Decode;
  reg      `EXC Exception;

   //inputs
  reg         Work;           // valid insn in pipeline
  reg   `INSN Insn;           // instruction bus
  reg  `PCADR PC;             // program counter (PC+4)
  reg  `DATAQ RegAValue,      // register Ra value from FRU
                RegBValue; 

  // internal signals
  wire     `REG RegA,
                RegB,
                RegDest;


   initial begin
      Const = 22'b0000_0000_0000_0000_0000_00;
      RegA_int = 6'b0000_00;      
      RegB_int = 6'b0000_00;
      RegDest_int = 6'b0000_00;
      Operand1 = 32'h0000_0000;
      Operand2= 32'h0000_0000;
      OpCode = 6'b0000_00;
      FctCode = 7'b0000_000;
      Decode = 6'b0000_00;
      Exception = 4'b0000;
      Work= 0;           // valid insn in pipeline
      Insn = 0;
           // instruction bus
      PC= 0;             // program counter (PC+4)
      RegAValue = 0;   // register Ra value from FRU
      RegBValue = 0; 
   end // initial begin

   always @(posedge Clk) begin
      Work = iWork;
      Insn = iInsn;
      PC = iPC;
      RegAValue = iRegAValue;
      RegBValue = iRegBValue;
   end // always @ (posedge Clk)
   
   
  //--------------------------------------------------------------------------
  // qualify register valid signals
  //--------------------------------------------------------------------------

  // RegA
  assign RegA`REGV = RegA_int`REGV & Work;
  assign RegA`REGN = RegA_int`REGN;
  // RegB
  assign RegB`REGV = RegB_int`REGV & Work;
  assign RegB`REGN = RegB_int`REGN;
  // RegDest
  assign RegDest`REGV = RegDest_int`REGV & Work;
  assign RegDest`REGN = RegDest_int`REGN;

  //--------------------------------------------------------------------------
  //
  // Update all outputs depending on instruction register
  //
  //--------------------------------------------------------------------------

  always @(Insn) begin
    //----------------------------------------------------------------------
    // opcode is always at same position (no mux needed)
    //----------------------------------------------------------------------
    OpCode = Insn[`POS_OPCODE];
  end

  always @(OpCode or Work) begin
    //----------------------------------------------------------------------
    // exceptions
    // figure out if we can deal with this insn
    //----------------------------------------------------------------------
    casez({Work, OpCode})
      // PAL codes
      7'b1_00_0000: Exception = `EXC_PAL; // PAL00
      7'b1_01_1??1: Exception = `EXC_PAL; // reserved PAL codes
      7'b1_01_1110: Exception = `EXC_PAL; // reserved PAL codes

      // reserved opcodes
      7'b1_00_0??1: Exception = `EXC_RESV;
      7'b1_0?_?100: Exception = `EXC_RESV;
      7'b1_00_??10: Exception = `EXC_RESV;
      7'b1_00_1101: Exception = `EXC_RESV;
      
      // floating point opcodes
      7'b1_?1_011?: Exception = `EXC_FP; 
      7'b1_?1_0101: Exception = `EXC_FP;
      7'b1_11_001?: Exception = `EXC_FP;
      7'b1_11_0001: Exception = `EXC_FP;
      7'b1_10_0???: Exception = `EXC_FP; // fp load/store

      // load locked / store conditional
      7'b1_10_1?1?: Exception = `EXC_LDLSTC; 

      default:      Exception = `EXC_NONE;
    endcase
  end

  //----------------------------------------------------------------------
  // generate the decode bits (internal processor encoding)
  // (see decode.v for definitions)
  //----------------------------------------------------------------------
`ifdef SILVER
  //
  // horizontal encoding (1 bit per insn class)
  //
  always @(OpCode or Work) begin
    if (Work) begin
      Decode[`DEC_ALU] = (OpCode[5:3] == 3'b010);  // ALU operation
      Decode[`DEC_MEM] = (OpCode[4:3] == 2'b01);   // memory operation
      Decode[`DEC_CTR] = (OpCode[5:4] == 2'b11) |
                          (OpCode[5:0] == 01_1010); // control transfer operation
    end else begin
      Decode[`DEC_HOR] = 'b0;
    end
  end
  //
  // vertical encoding (shared bits)
  //
  always @(OpCode or Decode[`DEC_HOR]) begin
    if (Decode[`DEC_ALU]) begin
      Decode[`DEC_ALU_MULT] = (OpCode[1:0] == 2'b11);
    end 
    else if (Decode[`DEC_MEM]) begin
      Decode[`DEC_MEM_ST]   = OpCode[2];
      Decode[`DEC_MEM_QW]   = OpCode[0];
      Decode[`DEC_MEM_ACC]  = ~({OpCode[5],OpCode[1]} == 2'b00);
    end 
    else if (Decode[`DEC_CTR]) begin
      Decode[`DEC_CTR_COND] = (OpCode[5:3] == 3'b11_1);
      Decode[`DEC_CTR_PC]   = (OpCode[5:4] == 2'b11);
    end
  end
`else
  always @(OpCode or Work) begin
    Decode = `FALSE;
    //
    // horizontal encoding (1 bit per insn class)
    case(OpCode)
      `OP_INTA,
      `OP_INTL,
      `OP_INTS,
      `OP_INTM:  Decode[`DEC_ALU] = `TRUE;
      `OP_LDA,
      `OP_LDAH,
      `OP_LDL,
      `OP_LDQ,
      `OP_LDL_L,
      `OP_LDQ_L,
      `OP_LDQ_U,
      `OP_STL,
      `OP_STQ,
      `OP_STL_C,
      `OP_STQ_C,
      `OP_STQ_U: Decode[`DEC_MEM] = `TRUE;
      `OP_BLBC,
      `OP_BEQ,
      `OP_BLT,
      `OP_BLE,
      `OP_BLBS,
      `OP_BNE,
      `OP_BGE,
      `OP_BGT,
      `OP_BR,
      `OP_BSR,
      `OP_JMP:   Decode[`DEC_CTR] = `TRUE;
    endcase
    //
    // vertical encoding (shared bits)
    case(OpCode)
      `OP_INTM:  Decode[`DEC_ALU_MULT] = `TRUE;
      `OP_STL,
      `OP_STQ,
      `OP_STL_C,
      `OP_STQ_C,
      `OP_STQ_U: Decode[`DEC_MEM_ST] = `TRUE;
      `OP_BLBC,
      `OP_BEQ,
      `OP_BLT,
      `OP_BLE,
      `OP_BLBS,
      `OP_BNE,
      `OP_BGE,
      `OP_BGT:   Decode[`DEC_CTR_COND] = `TRUE;
    endcase
    case(OpCode)
      `OP_LDA,
      `OP_LDAH,
      `OP_LDQ,
      `OP_LDQ_L,
      `OP_LDQ_U,
      `OP_STQ,
      `OP_STQ_C,
      `OP_STQ_U: Decode[`DEC_MEM_QW] = `TRUE;
      `OP_BLBC,
      `OP_BEQ,
      `OP_BLT,
      `OP_BLE,
      `OP_BLBS,
      `OP_BNE,
      `OP_BGE,
      `OP_BGT,
      `OP_BR,
      `OP_BSR:   Decode[`DEC_CTR_PC] = `TRUE;
    endcase
    case(OpCode)
      `OP_LDL,
      `OP_LDQ,
      `OP_LDL_L,
      `OP_LDQ_L,
      `OP_LDQ_U,
      `OP_STL,
      `OP_STQ,
      `OP_STL_C,
      `OP_STQ_C,
      `OP_STQ_U: Decode[`DEC_MEM_ACC] = `TRUE;
    endcase
    if (~Work) begin
      Decode[`DEC_HOR] = 'b0;
    end
  end
`endif

  always @(Insn or Decode) begin
    //----------------------------------------------------------------------
    // mux register numbers to IFU
    //----------------------------------------------------------------------
    //
    // Ra
    // always present in encoding (but might be dest, not source)
    if ((Decode[`DEC_CTR] & ~Decode[`DEC_CTR_COND]) | // uncond ctr
        (Decode[`DEC_MEM] & ~Decode[`DEC_MEM_ST]))    // ld
    begin
      RegA_int = {`FALSE, Insn[`POS_REGA]};
    end else begin
      RegA_int = {`TRUE, Insn[`POS_REGA]};
    end
    // 
    // Rb
    // can be missing
    if ((Decode[`DEC_CTR] & Decode[`DEC_CTR_PC]) | // bra encoding
        (Decode[`DEC_ALU] & Insn[12]))               // op immediate encoding was [12]
    begin
      // does not have Rb
      RegB_int = {`FALSE, Insn[`POS_REGB]};
    end else begin
      // all others have Rb
      RegB_int = {`TRUE, Insn[`POS_REGB]};
    end
    // 
    // RDest
    // can be Rc or Ra or missing
    if (Decode[`DEC_ALU]) begin
      // Rc for operate encoding
      RegDest_int [4:0] = Insn[`POS_REGC]; // [4:0] =`REGN
      if (Insn[`POS_REGC] == 31) begin  // used to be 31 instead of 31
        // r31 dest treated like no dest
        RegDest_int[5] = `FALSE;  // [5] = `REGV
      end else begin
        RegDest_int[5] = `TRUE;
      end
    end else if ((Decode[`DEC_MEM] & ~Decode[`DEC_MEM_ST]) |
                 (Decode[`DEC_CTR] & ~Decode[`DEC_CTR_COND])) begin
      RegDest_int[4:0] = Insn[`POS_REGA];  // RegDest_int`REGN did not work
      if (Insn[`POS_REGA] == 31) begin
        // r31 dest treated like no dest
        RegDest_int[5] = `FALSE; //`REGB = [5]
      end else begin
        RegDest_int[5] = `TRUE;
      end
    end else begin
      RegDest_int[5] = `FALSE;
    end

    //----------------------------------------------------------------------
    // mux function code bus to EXU
    //----------------------------------------------------------------------
    if (Decode[`DEC_CTR] & ~Decode[`DEC_CTR_PC]) begin
      FctCode = Insn[`POS_HINT];
    end 
    else begin
      FctCode = Insn[`POS_FUNCTION];
    end

    //----------------------------------------------------------------------
    // mux constant bus to EXU
    //----------------------------------------------------------------------
    //Const[21:0]= 'bx; // behavioral sanitizer  // [4:0] = `CONST
     if (Decode[`DEC_ALU]) begin
      if (Insn[12]) begin  // used to be 12
         // operate insn with immediate field
        Const[21] = `TRUE;   // [4] = `CONSTV
	 // POS_LITERAL was in place of POS_IMMEDIATE
        Const[20:0]/*`CONSTN*/ = Insn[`POS_IMMEDIATE]; // zero extended 
      end else begin
        // operate insn without immediate field
        Const[21]/*`CONSTV*/ = `FALSE;
      end
    end else if (Decode[`DEC_MEM]) begin
      // short displacement field
      Const[20:0]/*`CONSTN*/ = {{5{Insn[`POS_DISPHI]}},
                       Insn[`POS_DISP]}; // sign extended
    end else if (Decode[`DEC_CTR] & Decode[`DEC_CTR_PC]) begin
      // full displacement field
      Const[20:0]/*`CONSTN*/ = Insn[`POS_IMMEDIATE];
    end
  end

  //------------------------------------------------------------------------
  // mux operand busses to EXU
  //------------------------------------------------------------------------
   always @(Decode or RegAValue or RegBValue or PC) begin
    if (Decode[`DEC_CTR]) begin
      if (Decode[`DEC_CTR_COND]) begin
        Operand1 = RegAValue;
        Operand2 = PC;
      end else begin
        Operand1 = PC;
        Operand2 = RegBValue;
      end
    end else begin
      Operand1 = RegAValue;
      Operand2 = RegBValue;
    end
  end

endmodule // IDU_Logic





